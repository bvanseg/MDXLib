buildscript {
    repositories {
        mavenCentral()
        maven { url = "https://maven.minecraftforge.net/" }
        maven {
            url = "https://oss.sonatype.org/content/groups/public"
        }
        maven {
            url = "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath ('com.anatawa12.forge:ForgeGradle:2.3-1.0.+') {
            changing = true
        }
        classpath 'org.ajoberstar:gradle-git:0.10.1'
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.1.0"
    }
}

plugins {
  id "com.matthewprenger.cursegradle" version "1.1.0"
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.matthewprenger.cursegradle'
apply plugin: 'maven-publish'

configurations {
    deployerJars
}

dependencies {
    deployerJars 'org.apache.maven.wagon:wagon-ftp:2.2'
}

def version_minecraft="1.12.2"
def version_forge="14.23.5.2847"
def version_mappings="stable_39"
def curseforge_id="248278"
def curseforge_release_type="release"
def curseforge_api_key="${System.env.CURSEFORGE_API_KEY}"
def mavenUsername="${System.env.MAVEN_USER}"
def mavenPassword="${System.env.MAVEN_PASS}"

version = "${System.env.PROJECT_VERSION}"
group = "com.asx"
archivesBaseName = "mdxlib"

tasks.withType(JavaCompile) {
	sourceCompatibility = "1.8"
	targetCompatibility = "1.8"
	options.encoding = 'UTF-8'
}

tasks.withType(Jar) {
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

minecraft {
    version = "${version_minecraft}-${version_forge}"
    runDir = "run"
    mappings = version_mappings
    
    replace "@VERSION@", "${System.env.PROJECT_VERSION}" 
    replaceIn 'MDX.java'
}

println "Minecraft Forge ${version_forge} with mappings " + minecraft.mappings

def sharedManifest = {
    attributes 'FMLAT': 'mdxlib_at.cfg'
    attributes 'FMLCorePlugin' : 'com.asx.mdx.FMLModule'
	attributes 'FMLCorePluginContainsFMLMod' : 'true'
}

jar {
	archiveFileName = "${archivesBaseName}-${version}.jar"
    manifest sharedManifest
}

task srcJar(type: Jar) {
    from sourceSets.main.output
	archiveFileName = "${archivesBaseName}-${version}-sources.jar"
    classifier = 'sources'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
	archiveFileName = "${archivesBaseName}-${version}-deobf.jar"
    classifier = 'deobf'
    manifest sharedManifest
}

artifacts {
    archives srcJar, deobfJar
}

curseforge {
        apiKey = curseforge_api_key

        project {
            id = curseforge_id
            changelog = ''
            releaseType = hasProperty('curseforge_release_type') ? curseforge_release_type : 'release'

            mainArtifact jar

            relations {
                requiredLibrary 'mdxlib'
        }
    }
}

processResources {
    inputs.property "version", version
    inputs.property "mcversion", minecraft.version
	duplicatesStrategy = DuplicatesStrategy.INCLUDE

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':version, 'mcversion':minecraft.version
    }
    
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                properties = [
                        forgeVersion: version_minecraft,
                        mcVersion: minecraft.version
                ]
            }
        }
    }

    repositories {
        maven {
            credentials {
                username mavenUsername
                password mavenPassword
            }
            url = "ftp://maven.aliensvspredator.org"
        }
    }
}

//task("publishToMaven", dependsOn:"build") {
//    description = "Uploads the artifacts generated during the build process to the aliensvspredator maven repository"
//
//    uploadArchives {
//        repositories.mavenDeployer {
//            configuration = configurations.deployerJars
//            repository(url: "ftp://maven.aliensvspredator.org") {
//                authentication(userName: mavenUsername, password: mavenPassword)
//            }
//
//	        pom.project {
//	        	properties {
//	            	forgeVersion = version_minecraft
//	            	mcVersion = minecraft.version
//	            }
//	        }
//        }
//    }
//}